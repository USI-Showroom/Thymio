<!DOCTYPE aesl-source>
<network>


<!--list of global events-->
<event name="sendDirection" size="1"/>


<!--node USI-1-->
<node name="USI-1" nodeId="{6ae0262a-203b-44a4-88c0-6805a56e33d6}"><![CDATA[# USI Showroom - remoteDrive (vehicle)
# Control a Thymio with another one. This is the code for the vehicle.
# Version 05.04.2020
# Direction numbering starts with 1 for forward
# and then rotates clockwise

# Movement speed
var speed = 200

# Turning factor, increase to make sharper turns
var turnFactor = 3

call leds.top(32, 0, 0)
call leds.circle(0, 0, 0, 0, 0, 0, 0, 0)

onevent sendDirection
if event.args[0] == 0 then
	motor.left.target = 0
	motor.right.target = 0
	call leds.top(32, 0, 0)
	call leds.circle(0, 0, 0, 0, 0, 0, 0, 0)
elseif event.args[0] == 1 then
	motor.left.target = speed
	motor.right.target = speed
	call leds.top(0, 32, 0)
	call leds.circle(32, 0, 0, 0, 0, 0, 0, 0)
elseif event.args[0] == 2 then
	motor.left.target = speed
	motor.right.target = speed / turnFactor
	call leds.top(0, 32, 0)
	call leds.circle(0, 0, 32, 0, 0, 0, 0, 0)
elseif event.args[0] == 3 then
	motor.left.target = - speed
	motor.right.target = - speed
	call leds.top(0, 32, 0)
	call leds.circle(0, 0, 0, 0, 32, 0, 0, 0)
elseif event.args[0] == 4 then
	motor.left.target = speed / turnFactor
	motor.right.target = speed
	call leds.top(0, 32, 0)
	call leds.circle(0, 0, 0, 0, 0, 0, 32, 0)
end]]></node>


<!--node USI-2-->
<node name="USI-2" nodeId="{d0682387-7a2d-4046-bb33-4c85dd228449}"><![CDATA[# USI Showroom - remoteDrive (controller)
# Control a Thymio with another one. This is the code for the controller.
# Version 05.04.2020
# Direction numbering starts with 1 for forward
# and then rotates clockwise

call leds.circle(0, 0, 0, 0, 0, 0, 0, 0)

onevent buttons
if button.center == 1 then
	emit sendDirection 0
	call leds.circle(0, 0, 0, 0, 0, 0, 0, 0)
elseif button.forward == 1 then
	emit sendDirection 1
	call leds.circle(32, 0, 0, 0, 0, 0, 0, 0)
elseif button.right == 1 then
	emit sendDirection 2
	call leds.circle(0, 0, 32, 0, 0, 0, 0, 0)
elseif button.backward == 1 then
	emit sendDirection 3
	call leds.circle(0, 0, 0, 0, 32, 0, 0, 0)
elseif button.left == 1 then
	emit sendDirection 4
	call leds.circle(0, 0, 0, 0, 0, 0, 32, 0)
end]]></node>


</network>
